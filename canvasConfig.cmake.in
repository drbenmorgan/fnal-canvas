# - canvasConfig Template file

# - May not need this variable as versioning file
# holds details, but seems to be convention
set(canvas_VERSION @canvas_VERSION@)

#-----------------------------------------------------------------------
# - CMakePackageConfigHelpers Startup...
@PACKAGE_INIT@
#-----------------------------------------------------------------------
# - CMakePackageConfigHelpers End...

# Don't *strictly* need include dir(s) as linking to canvas lib
# targets will propagate all needed requirements. However, it's
# useful if we're using header-only parts and don't want to
# have a binary linkage.
set_and_check(canvas_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")

# - When needed, refind needed targets here
# - Assert EXACT version as this is default behaviour of UPS setups.
find_package(Boost @Boost_MAJOR_VERSION@.@Boost_MINOR_VERSION@ EXACT REQUIRED)
find_package(CLHEP @CLHEP_VERSION@ EXACT REQUIRED)
find_package(cetlib @cetlib_VERSION@ EXACT REQUIRED)
find_package(fhiclcpp @fhiclcpp_VERSION@ EXACT REQUIRED)
find_package(messagefacility @messagefacility_VERSION@ EXACT REQUIRED)

# Temporary local findmodule for range
list(INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_LIST_DIR}")
find_package(Range-v3 REQUIRED NO_CONFIG)
list(REMOVE_AT CMAKE_MODULE_PATH 0)

# ROOT only if we built with Clang
# Possibly not needed depending on linker/rpath settings.
set(canvas_NEED_ROOT_INTROSPECTION @canvas_NEED_ROOT_INTROSPECTION@)
if(canvas_NEED_ROOT_INTROSPECTION)
  find_package(ROOT @ROOT_VERSION@ EXACT REQUIRED)
endif()

find_package(TBB @TBB_VERSION@ EXACT REQUIRED)

# - Include our targets
include("${CMAKE_CURRENT_LIST_DIR}/canvasTargets.cmake")


